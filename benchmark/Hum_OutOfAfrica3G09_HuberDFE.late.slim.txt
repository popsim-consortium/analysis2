initialize() {
    initializeMutationRate(2.35e-8);

    // m0 mutation type: neutral
    initializeMutationType("m0", 0.5, "f", 0.0);
    // m1 mutation type: nonneutral
    initializeMutationType("m1", 0.5, "g", -0.014, 0.19);

    initializeGenomicElementType("g0", c(m0,m1), c(0.3, 0.7));

    if (exists("SEQSIZE")){
	// Simulating simple uniform stretch of exon
	initializeGenomicElement(g0, 0, asInteger(SEQSIZE)-1);
    } else if (exists("CHROMOSOME")){
	// Simulating chromosomes
        lines = readFile(CHROMOSOME);
        for (line in lines) {
            components = strsplit(line, "\t");
            start = asInteger(components[1]);
            end = asInteger(components[2]);
            initializeGenomicElement(g0, start, end); 
        }
    }

    if (exists("RECOMBINATION")) {
        prev_end = 0;
        prev_rate = 0;
        recomb_rates = c(0);
        ends = c(0);
        lines = readFile(RECOMBINATION);
        for (line in lines) {
            components = strsplit(line, "\t");
            curr_end = asInteger(components[0]);
            curr_rate = asFloat(components[1]);
            recomb_rates = c(recomb_rates, (curr_rate-prev_rate)*0.01/(curr_end-prev_end)*FACTOR);
            ends = c(ends, curr_end);
            prev_end = curr_end;
            prev_rate = curr_rate;
        }
        initializeRecombinationRate(recomb_rates, ends);
    } else {
        // Rescale mutation rate to keep constant rho in simulations
        // Assume 1 cM/Mb (rough from Comeron 2012). 
        // Factor of 0.5 is for no crossing-over in males
        initializeRecombinationRate(1.1485597641285933e-08);
    }
}

// Burn-in 10Na
// Create the ancestral African population
1 { sim.addSubpop("p0", 7300); }

// Expand the African population to 12300
// This occurs 220,000 years (8800) generations ago
73001 late() { p0.setSubpopulationSize(12300); }

// Split non-Africans from Africans and set up migration between them
// This occurs 140,000 years (5600 generations) ago
76201 late() {
    sim.addSubpopSplit("p1", 2100, p0);
    p0.setMigrationRates(c(p1), c(25e-5));
    p1.setMigrationRates(c(p0), c(25e-5));
}

// Split p2 into European and East Asian subpopulations
// This occurs 21,200 years (848 generations) ago
80953 late() {
    sim.addSubpopSplit("p2", 510, p1);
    p1.setSubpopulationSize(1000); // reduce European size
    // Set migration rates for the rest of the simulation
    p0.setMigrationRates(c(p1, p2), c(3e-5, 1.9e-5));
    p1.setMigrationRates(c(p0, p2), c(3e-5, 9.6e-5));
    p2.setMigrationRates(c(p0, p1), c(1.9e-5, 9.6e-5));
}

// Set up exponential growth in Europe and East Asia
// Where N(0) is the base subpopulation size and t = gen - 80953:
// N(Europe) should be int(round(N(0) * e^(0.004*t)))
// N(East Asia) should be int(round(N(0) * e^(0.0055*t)))
80954:81801 late() {
    t = sim.generation - 80953;
    p1_size = round(1000 * exp(0.004 * t));
    p2_size = round(510 * exp(0.0055 * t));
    p1.setSubpopulationSize(asInteger(p1_size));
    p2.setSubpopulationSize(asInteger(p2_size));
}

// Generation 8800 is the present. Output and terminate.
81801 late() {
    p0.outputSample(100, filePath='p0.late.slimout'); // YRI phase 3 sample of size 108 individuals
    p1.outputSample(100, filePath='p1.late.slimout'); // CEU phase 3 sample of size 99 individuals
    p2.outputSample(100, filePath='p2.late.slimout'); // CHB phase 3 sample of size 103 individuals
}
